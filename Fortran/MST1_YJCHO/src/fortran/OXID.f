C...................................................................... 
C.....PROGRAM OXID CALCULATES THE FRACTION OF OXIDATION OF STEAM
C.....AND CO2 IN A SPHERICAL BUBBLE AFTER RISING THROUGH A MELT
C.....LAYER OF FINITE DEPTH DUE TO OXIDATION OF THE MELT.  INPUT
C.....INFORMATION IS P=SYSTEM PRESSURE IN MPA, TEMPC=INITIAL GAS
C.....TEMPERATURE IN BUBBLE UPON RELEASE FROM SURFACE, TEMPM=MELT
C.....TEMPERATURE, D=BUBBLE DIAMETER, DEPTH=DEPTH OF MELT LAYER,
C.....AND UT=BUBBLE TERMINAL RISE VELOCITY.  OXH2O AND OXCO2 ARE
C.....THE CALCULATED FRACTIONS OF H2O AND CO2 WHICH ARE OXIDIZED AS     
C.....THE BUBBLE ASSENDS THROUGH THE LAYER.
C...................................................................... 
      SUBROUTINE OXID(P,TEMPC,TEMPM,D,DEPTH,UT,OXH2O,OXCO2)   
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      CONV=1.D-4      
      TCONI=300.D0    
      PI=3.141592654D0
      TASIGN=TEMPC    
      IF(TASIGN.LT.TCONI) TASIGN=TCONI    
      TEMP1=TASIGN    
      TEMP2=TEMP1     
      CALL TTODAB(TEMP1,TEMP2,DAB1,DAB2,P)
      CH2O=(DAB1*DEPTH)/(UT*D*D)
      CCO2=(DAB2*DEPTH)/(UT*D*D)
      SUMH2O=0.D0     
      SUMCO2=0.D0     
      DO 70 KD=1,10   
      SMH2LD=SUMH2O   
      SMCOLD=SUMCO2   
      RN2=(FLOAT(KD)*PI)*(FLOAT(KD)*PI)   
      ARH2O=4.D0*RN2*CH2O
      ARCO2=4.D0*RN2*CCO2
      IF(ARH2O.GT.100.D0.OR.ARCO2.GT.100.D0) GO TO 100
      SUMH2O=SUMH2O+6.D0/(RN2*DEXP(ARH2O)) 
      SUMCO2=SUMCO2+6.D0/(RN2*DEXP(ARCO2)) 
      DSH2O=ABS(SUMH2O-SMH2LD)  
      DSCO2=ABS(SUMCO2-SMCOLD)  
      IF(DSH2O.LE.CONV.AND.DSCO2.LE.CONV) GO TO 100 
   70 CONTINUE
  100 CONTINUE
      OXH2O=1.D0-SUMH2O
      OXCO2=1.D0-SUMCO2
      OXH2O=DMAX1(0.D0,OXH2O)   
      OXCO2=DMAX1(0.D0,OXCO2)   
      IF(OXH2O.GT.1.D0) OXH2O=1.D0
      IF(OXCO2.GT.1.D0) OXCO2=1.D0
      RETURN
      END