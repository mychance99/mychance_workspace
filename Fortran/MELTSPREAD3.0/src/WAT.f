C...................................................................... 
C.....SUBROUTINE WAT EVALUATES THE MELT-TO-WATER HEAT TRANSFER
C.....COEFFICIENT.    
C...................................................................... 
      SUBROUTINE WAT(TWABS,TSUB,JG,HTOT,PDRYWL,HMLT) 
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      REAL(8) LAMDAC,KL,KV,KM,KNC,JG
      COMMON /BLKHT/KM,PM,CM,UM,EMIS,SIGMAM,KNC,PNC,CNC,UNC
      COMMON/PROPI/ NMATC,NMATFI,NMATFF,ICAOH2,ICACO3,IMCCO3,IFH2O,     
     1IVH2O,ICK2O,IVK2O,INA2O,ITIO2,ISIO2,ICAO,IMGO,IAL2O3,IFEO,IFE2O3, 
     2IFE3O4,IFE,ICR,INI,IZR,IU,IB4C,IB,ICR2O3,INIO,IUO2,IZRO2,IB2O3,   
     3NBCINT(999),NODES(999),NUMNOD,IMOX(28),IBAS,ILCS,ILL,NACTIV(999),     
     4NBFRZM,NBFRZO,NABLFM,NDRNFM,NTYPMT(999,999),NDOOR,NBMADJ,NUMSHV,   
     5NBSHL(999),NCRSTS(999),NUMSHH,NSKIPE,NACSH,NWAT,NPEND,NBCBOT,
     6NCRTOP,NBFZOE,NBFZME,NGEOM,NL(999,999),NFRSCT,NLOGSH,NADAB,NINVIS,
     7NSIMST,NSTEEL,NOVHT,NOVTK,NOVUM,NOVEM,NOVSIG,NPLLOC(999),NBPL,
     8IXP(999),IYP(999),NVTPE,NSOLTP,NSOLF,NINTF,IFLGA(999),ICTYPE,ICTC,
     9NPOURS,NSMP,NPED,NDOR,NSHL,NANULS,NSMPCV,NBOIL,NSWALL,NMVER,
     1NCRTEM,NTHINC,NVELP,NITMAX,NENMAX,NPRINT,NPFREQ,NTIMSPC,NPLFREQ,
     2NPLTOT,NJET,NJETP,NJETD,NJETND,NODCAP,NVELPW,NITMAXW,NENMXW,
     3IFLGJ(999),NBEDCQ,ISHELE
      GRAV=9.82D0     
      STEF=5.67D-8    
      PI=3.141592654D0
      E13=1.D0/3.D0   
      E23=2.D0/3.D0   
      CD=4.D0/3.D0
C.....CHECK TO SEE IF DEBRIS IS ABOVE SATURATION TEMPERATURE OR 
C.....CALCULATION IS ADIABATIC.
      HTOT=0.D0
      IF(TWABS.LE.TSAT) GO TO 105
      IF(NADAB.EQ.1) GO TO 105
C.....FIRST EVALUATE WATER PROPERTIES
      CALL CONWAT(TSAT,KL,PL,CL,UL,HLV,EWL,EWV,CWV,SIGMA,PDRYWL)
C.....EVALUATE VAPOR PROPERTIES.      
      CALL CONH2O(TSAT,PDRYWL,CV,UV,KV,PV)
C.....EVALUATE SCALING PARAMETERS.
      TSUP=TWABS-TSAT 
      ALPHAL=KL/(PL*CL)
      DP=PL-PV
      LAMDAC=2.D0*PI*SQRT(SIGMA/(GRAV*DP))
      F2=(2.D0*PI*SIGMA)/((PL+PV)*LAMDAC*LAMDAC*LAMDAC)
      F12=F2**0.25D0  
      QSUB=(2.D0*KL*TSUB*F12)/SQRT(PI*ALPHAL)
      UP1=HLV*PV*GRAV*DP*KV*KV*KV
      UP2=UV*SQRT(SIGMA/(GRAV*DP))
      HFBO=0.425D0*(UP1/UP2)**0.25D0      
      ASTAR=1.D0
      IF(JG.LE.0.D0.OR.HMLT.LE.0.D0) GO TO 660
      UINF=((8.D0*SIGMAM)/(3.D0*CD*PM))**0.5D0
      ASTAR=1.D0+(4.50*JG)/UINF 
      ASTAR=DMIN1(8.D0,ASTAR)
  660 CONTINUE
C.....DETERMINE WHETHER THE BOILING REGIME IS NUCLEATE, TRANSITION,     
C.....OR FILM BOILING.  FIRST FIND CHF USING IVEY-MORRIS EQUATION.      
      C1=1.D0+(0.1D0*((PL/PV)**0.75D0)*CL*TSUB)/HLV 
      C2=0.14D0*PV*HLV*(((GRAV*SIGMA*DP)/PV**2)**0.25D0)      
      QCHF=C1*C2      
      HCHF=QCHF/TSUP  
      C3=SQRT(SIGMA/(GRAV*DP))  
      C4=((1.25D5*UL*HLV)/C3)*(CL/HLV)**3 
      C5=1.D0/((CL*UL)/KL)**5.1D0
      ANB=C4*C5
      DTCHF=(QCHF/ANB)**E13     
C.....CALCULATE MFB TEMPERATURE USING HENRY'S CORRELATION.    
      C6=(0.127D0*PV*HLV)/KV    
      C7=((GRAV*DP)/(PL+PV))**E23
      C8=(UV/(GRAV*DP))**E13    
      DTBER=C6*C7*C3*C8
      C9=SQRT((KL*PL*CL)/(KM*PM*CM))      
      C10=((C9*HLV)/(CM*DTBER))**0.6D0    
      DTMIN=DTBER+0.42D0*C10*(DTBER+TSUB) 
      IF(DTMIN-DTCHF.LT.1.D0) DTMIN=DTCHF+1.D0
      IF(TSUP.GT.DTCHF) GO TO 500
C.....BOILING REGIME IS NUCLEATE; FIND THE HEAT TRANSFER COEFFICIENT    
C.....USING ROHSENOW'S CORRELATION.
      HTOT=ANB*TSUP*TSUP
      GO TO 105
  500 CONTINUE
      IF(TSUP.GT.DTMIN) GO TO 510
C.....BOILING REGIME IS TRANSITION; FIND HEAT TRANSER COEFFICIENT USING 
C.....IVEY-MORRIS EQUATION.     
      TWABSM=DTMIN+TSAT
      QRADM=STEF*EMIS*(TWABSM**4-TSAT**4) 
      QOM=HFBO*DTMIN**0.75D0    
      ALPHAM=2.D0*SQRT(QSUB/QOM)-QRADM/QOM
      CALL FILMO(ALPHAM,DEL,ASTAR)
      QMIN=ASTAR*(QOM/DEL+QRADM)
      QTRAN=((QMIN-QCHF)/(DTMIN-DTCHF))*(TSUP-DTCHF)+QCHF     
      HTOT=QTRAN/TSUP 
      GO TO 105
  510 CONTINUE
C.....BOILING REGIME IS FILM; FIND THE HEAT TRANSFER COEFFICIENT.
      QRAD=STEF*EMIS*(TWABS**4-TSAT**4)   
      HRAD=QRAD/TSUP  
      HFB=HFBO/TSUP**0.25D0     
      DELO=KV/HFB     
      QO=HFB*TSUP     
      ALPHA=2.D0*SQRT(QSUB/QO)-QRAD/QO    
      CALL FILMO(ALPHA,DEL,ASTAR)
      HTOT=ASTAR*(HFB/DEL+HRAD)
  105 CONTINUE
      RETURN
      END