C...................................................................... 
C.....GIVEN THE SPECIFIC ENTHALYPY AND THE MAXIMUM SPECIFIC ENTHALPY    
C.....THAT HAS BEEN OBTAINED FOR CONCRETE, THIS ROUTINE CALCULATES      
C.....THE CONCRETE TEMPERATURE AND THE DERIVATIVE OF THE TEMPERATURE    
C.....WITH RESPECT TO THE SPECIFIC ENTHALPY. THE EFFECTS OF RELEASE     
C.....OF FREE WATER, BOUND WATER, AND CARBON DIOXIDE AS WELL AS THE     
C.....EFFECTS OF CONCRETE MELTING ARE ALL REPRESENTED. THE MAXIMUM      
C.....CONCRETE SPECIFIC ENTHALPY THAT HAS BEEN ATTAINED IS USED TO      
C.....CHARACTERIZE THE STATE OF THE CONCRETE WITH RESPECT TO  
C.....DECOMPOSITION AND RELEASE OF WATER AS WELL AS CARBON DIOXIDE.     
C...................................................................... 
      SUBROUTINE TEC(E,EMAX,T,DTDE,RMASS,YCACO3,YMCCO3,YCAOH2,YFH2O,NROT
     1)     
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      COMMON/PROPI/ NMATC,NMATFI,NMATFF,ICAOH2,ICACO3,IMCCO3,IFH2O,     
     1IVH2O,ICK2O,IVK2O,INA2O,ITIO2,ISIO2,ICAO,IMGO,IAL2O3,IFEO,IFE2O3, 
     2IFE3O4,IFE,ICR,INI,IZR,IU,IB4C,IB,ICR2O3,INIO,IUO2,IZRO2,IB2O3,   
     3NBCINT(999),NODES(999),NUMNOD,IMOX(28),IBAS,ILCS,ILL,NACTIV(999),     
     4NBFRZM,NBFRZO,NABLFM,NDRNFM,NTYPMT(999,999),NDOOR,NBMADJ,NUMSHV,   
     5NBSHL(999),NCRSTS(999),NUMSHH,NSKIPE,NACSH,NWAT,NPEND,NBCBOT,
     6NCRTOP,NBFZOE,NBFZME,NGEOM,NL(999,999),NFRSCT,NLOGSH,NADAB,NINVIS,
     7NSIMST,NSTEEL,NOVHT,NOVTK,NOVUM,NOVEM,NOVSIG,NPLLOC(999),NBPL,
     8IXP(999),IYP(999),NVTPE,NSOLTP,NSOLF,NINTF,IFLGA(999),ICTYPE,ICTC,
     9NPOURS,NSMP,NPED,NDOR,NSHL,NANULS,NSMPCV,NBOIL,NSWALL,NMVER,
     1NCRTEM,NTHINC,NVELP,NITMAX,NENMAX,NPRINT,NPFREQ,NTIMSPC,NPLFREQ,
     2NPLTOT,NJET,NJETP,NJETD,NJETND,NODCAP,NVELPW,NITMAXW,NENMXW,
     3IFLGJ(999),NBEDCQ,ISHELE
      COMMON/PRINTR/ QFEH2O,QCRH2O,QZRH2O,QFECO2,QCRCO2,QZRCO2,
     4XFH2OU,XFCAOH,XFMGCA,XFCACO,XZRMRE,XFEMRE,XCRMRE,XZRORE,XFEORE,   
     5XCRORE,XMH2O,XMCO2,XMCACO,XMMGCA,XMCAOH,TFWL,TFWS,TBWL,TBWS,      
     6TMCAL,TMCAS,TCAL,TCAS,TFOS,TFOL,TFMS,TFML,XVISC(28),SVISC(28),    
     7XMOL(28),FMMOL(28),ROM(28),ROMLIQ(28),AEQM(28,2),BEQM(28,2),
     8CEQM(28,2),ECL,ECS,ECAL,ECAS,EMCAL,EMCAS,EBWL,EBWS,EFWL,EFWS,    
     9STEF,GRAV,PI,TCS,TCL,CCL,CCS,RMASSL,WPCC,WPM,WPA,WPCS,ROC,RMASSS,   
     1ANGSHL,RSLAGL,RSLAGS,HNODT,VFAV,VGAV,QXAV,XWTSS(16),  
     2TSCS(2),TSCL(2),ESCS(2),ESCL(2),CCSS(2),CCSL(2),ROSTLS,ROSTLL,
     3XFRGAS,HMINC,TST(99),TSTOP(99),AINTP(99),BINTP(99),DRATIO(999),
     4XBCN(999),XDCN(999),XBLT(15),ADEC(99),BDEC(99),APOUR(16,99),
     5BPOUR(16,99),XWTC(14),BWIDTH,THCKCV,TMBOIL,TEBOIL,VFINT,ANGFAN,
     6ALPMAX,THSHL,XFCABL,XNDMIN,DVMX,DAVMX,DEAVMX,DEMX,TDC,QDCU,QDCUO2,
     7TSINJ,EINJ,DTINJ,TKINJ,ROEV,CPINJ,SURFT,VSINJ,EMINJ,TSINJO,EINJO,
     8DTINJO,TKINJO,ROEVO,CPINJO,SURFTO,VSINJO,EMINJO,XLEADE,ARSUM,
     9DBUBOX,UTRISE,TREMSH,DJET,DFALL,WEBER,FROUDE,XPSAITO,XPEPSTN,
     1FRAG,XMBEDT,XMBED(16),XLPENT,XLPENA,UJET,UFALL,HFALL,ERPV,
     2TJETT(99),DJETT(99),HWATP,XDOTJET,DVMXW,DAVMXW,DEMXW,DEAVMXW,
     3QJETW,XSTMJF,TINTSJF,ESAT,DRDOOR,DRANL,EI,QDCBUO2,QDCBU,
     4EBEDS,EBEDB
      DATA ZERO / 0.0 /
      DATA ONE / 1.0 /
      DATA TWO / 2.0 /
      DATA HALF / 0.5 /
C.....CHECK FOR STEEL SURFACES. 
      IF(NROT.NE.2) GO TO 7631  
      CALL TESS(E,T,DTDE)
      GO TO 1000      
 7631 CONTINUE
C.....CALCULATE THE NEW MAXIMUM SPECIFIC ENTHALPY.  
C     IF (E .GT. EMAX) EMAX=E   
C.....CALCULATE THE NEW FACTORS THAT CHARACTERIZE THE STATE OF
C.....CONCRETE DECOMPOSITION AND RELEASE OF WATER AS WELL AS  
C.....CARBON DIOXIDE. 
      YCACO3=ONE      
      IF (EMAX .GT. ECAS) YCACO3=(ECAL-EMAX)/(ECAL-ECAS)      
      IF (EMAX .GE. ECAL) YCACO3=ZERO     
      ZCACO3=ONE-YCACO3
      DCACO3=ZERO     
      IF (E .EQ. EMAX .AND. E .GT. ECAS .AND. E .LT. ECAL)    
     1DCACO3=-ONE/(ECAL-ECAS)   
      YMCCO3=ONE      
      IF (EMAX .GT. EMCAS) YMCCO3=(EMCAL-EMAX)/(EMCAL-EMCAS)  
      IF (EMAX .GE. EMCAL) YMCCO3=ZERO    
      ZMCCO3=ONE-YMCCO3
      DMCCO3=ZERO     
      IF (E .EQ. EMAX .AND. E .GT. EMCAS .AND. E .LT. EMCAL)  
     1DMCCO3=-ONE/(EMCAL-EMCAS) 
      YCAOH2=ONE      
      IF (EMAX .GT. EBWS) YCAOH2=(EBWL-EMAX)/(EBWL-EBWS)      
      IF (EMAX .GE. EBWL) YCAOH2=ZERO     
      ZCAOH2=ONE-YCAOH2
      DCAOH2=ZERO     
      IF (E .EQ. EMAX .AND. E .GT. EBWS .AND. E .LT. EBWL)    
     1DCAOH2=-ONE/(EBWL-EBWS)   
      YFH2O=ONE
      IF (EMAX .GT. EFWS) YFH2O=(EFWL-EMAX)/(EFWL-EFWS)
      IF (EMAX .GE. EFWL) YFH2O=ZERO      
      ZFH2O=ONE-YFH2O 
      DFH2O=ZERO      
      IF (E .EQ. EMAX .AND. E .GT. EFWS .AND. E .LT. EFWL)    
     1DFH2O=-ONE/(EFWL-EFWS)    
C.....CALCULATE THE TEMPERATURE OF MOLTEN CONCRETE. 
      IF (E .LT. ECL) GO TO 100 
      DEDT=CCL
      DTDE=ABS(ONE/DEDT)
      T=TCL+DTDE*(E-ECL)
      RMASS=RMASSL    
      GO TO 1000      
C.....CALCULATE THE TEMPERATURE OF CONCRETE BETWEEN THE SOLIDUS
C.....AND THE LIQUIDUS.
  100 IF (E .LT. ECS) GO TO 200 
      DEDT=(ECL-ECS)/(TCL-TCS)  
      DTDE=ABS(ONE/DEDT)
      T=TCS+DTDE*(E-ECS)
      RMASS=RMASSL    
      GO TO 1000      
C.....CALCULATE THE TEMPERATURE OF SOLID CONCRETE BELOW THE   
C.....SOLIDUS TEMPERATURE.      
  200 A=ZERO
      B=ZERO
      C=ZERO
      DADE=ZERO
      DBDE=ZERO
      DCDE=ZERO
      RMASSN=ZERO     
      RMASSD=ZERO     
      DO 299 I=1,NMATC
      XYI=XMOL(I)     
      IF (I .EQ. ICACO3) XYI=XYI*YCACO3   
      IF (I .EQ. ICK2O) XYI=XYI*YCACO3    
      IF (I .EQ. IMCCO3) XYI=XYI*YMCCO3   
      IF (I .EQ. ICAOH2) XYI=XYI*YCAOH2   
      IF (I .EQ. IFH2O) XYI=XYI*YFH2O     
      IF (I .EQ. ICAO) XYI=XYI+XMOL(ICACO3)*ZCACO3  
     1+XMOL(IMCCO3)*ZMCCO3+XMOL(ICAOH2)*ZCAOH2      
      IF (I .EQ. IVK2O) XYI=XYI+XMOL(ICK2O)*ZCACO3  
      IF (I .EQ. IMGO) XYI=XYI+XMOL(IMCCO3)*ZMCCO3  
      IF (I .EQ. IVH2O) XYI=XYI+XMOL(IFH2O)*ZFH2O   
      A=A+XYI*AEQM(I,1)
      B=B+XYI*BEQM(I,1)
      C=C+XYI*CEQM(I,1)-E*XYI*FMMOL(I)    
      DCDE=DCDE-XYI*FMMOL(I)    
      RMASSN=RMASSN+XYI*FMMOL(I)
      RMASSD=RMASSD+XMOL(I)*FMMOL(I)      
  299 CONTINUE
      RMASS=RMASSN/RMASSD
C.....IF THE SPECIFIC ENTHALPY IS LESS THAN THE MAXIMUM SPECIFIC
C.....ENTHALPY, THEN DECOMPOSITION REACTIONS ARE NOT TAKING PLACE
C.....AND THE FOLLOWING STATEMENTS MAY BE SKIPPED.  
      IF (E .LT. EMAX) GO TO 400
      I=ICACO3
      IMOX(I)=1
      XMOLI=XMOL(I)   
      DADE=DADE+XMOLI*DCACO3*(AEQM(I,1)-AEQM(ICAO,1))
      DBDE=DBDE+XMOLI*DCACO3*(BEQM(I,1)-BEQM(ICAO,1))
      DCDE=DCDE+XMOLI*DCACO3*(CEQM(I,1)-CEQM(ICAO,1))
     1-E*XMOLI*DCACO3*(FMMOL(I)-FMMOL(ICAO))
      DADE=DADE+XMOLI*DCACO3*(AEQM(ICK2O,1)-AEQM(IVK2O,1))    
      DBDE=DBDE+XMOLI*DCACO3*(BEQM(ICK2O,1)-BEQM(IVK2O,1))    
      DCDE=DCDE+XMOLI*DCACO3*(CEQM(ICK2O,1)-CEQM(IVK2O,1))    
     1-E*XMOLI*DCACO3*(FMMOL(ICK2O)-FMMOL(IVK2O))   
      I=IMCCO3
      IMOX(I)=1
      XMOLI=XMOL(I)   
      DADE=DADE+XMOLI*DMCCO3*(AEQM(I,1)-AEQM(ICAO,1)-AEQM(IMGO,1))      
      IMOX(I)=1
      DBDE=DBDE+XMOLI*DMCCO3*(BEQM(I,1)-BEQM(ICAO,1)-BEQM(IMGO,1))      
      DCDE=DCDE+XMOLI*DMCCO3*(CEQM(I,1)-CEQM(ICAO,1)-CEQM(IMGO,1))      
     1-E*XMOLI*DMCCO3*(FMMOL(I)-FMMOL(ICAO)-FMMOL(IMGO))      
      I=ICAOH2
      IMOX(I)=1
      XMOLI=XMOL(I)   
      DADE=DADE+XMOLI*DCAOH2*(AEQM(I,1)-AEQM(ICAO,1))
      DBDE=DBDE+XMOLI*DCAOH2*(BEQM(I,1)-BEQM(ICAO,1))
      DCDE=DCDE+XMOLI*DCAOH2*(CEQM(I,1)-CEQM(ICAO,1))
      DCDE=DCDE-E*XMOLI*DCAOH2*(FMMOL(I)-FMMOL(ICAO))
      I=IFH2O
      XMOLI=XMOL(I)   
      DADE=DADE+XMOLI*DFH2O*(AEQM(I,1)-AEQM(IVH2O,1))
      DBDE=DBDE+XMOLI*DFH2O*(BEQM(I,1)-BEQM(IVH2O,1))
      DCDE=DCDE+XMOLI*DFH2O*(CEQM(I,1)-CEQM(IVH2O,1))
     1-E*XMOLI*DFH2O*(FMMOL(I)-FMMOL(IVH2O))
  400 CONTINUE
      T=-C/B
      IF (A .EQ. ZERO) GO TO 500
      BR2A=HALF*B/A   
      CRA=C/A
      T=-BR2A+SQRT(BR2A*BR2A-CRA)
  500 CONTINUE
      DEDT=-(TWO*A*T+B)/(DADE*T*T+DBDE*T+DCDE)      
      DEDT=ABS(DEDT)  
      DTDE=ABS(ONE/DEDT)
 1000 CONTINUE
      RETURN
      END