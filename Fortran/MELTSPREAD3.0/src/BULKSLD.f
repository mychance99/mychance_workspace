C...................................................................... 
C.....THE FOLLOWING SUBROUTINE CHECKS TO DETERMINE IF THE BULK MELT      
C.....TEMPERATURE HAS DROPPED BELOW THE SOLIDUS TEMPERATURE FOR
C.....EITHER THE METALLIC OR OXIDIC PHASES.  IF SO, AND BULK FREEZING   
C.....IS SPECIFIED, THE SOLIDIFIED MATERIAL IS PRECIPITATED OUT ONTO    
C.....THE BASEMAT SURFACE WHERE IT IS NODALIZED INTO THE BASEMAT VECTOR.
C...................................................................... 
      SUBROUTINE BULKSLD     
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION HSEND(28)
      COMMON/CATCHR/A(999),B(999),C(999),D(999),P(999),Q(999),
     1RECON(999),DELVEL(999),DSRDT(999),DELHTS(999),EMAX(999,999),
     2DBLKEN(999),DELHIT(16,999),SOURCE(16,999),SRCTOT(999),
     3HCROLD(16,999),DCDOT(999),HCRUST(16,999),DFILM(16,999),
     4DFMOLD(16,999),DENOLD(999),SIGOX(999),SIGCON(999),BET(999),
     5TFZX(999),TFZC(999),TKX(999),TKM(999),THETO(999,999),
     6THET1(999,999),OMEG0(4,999),OMEG1(4,999),TAO0(999),TAO1(999),
     7SIG0(999),SIG1(999),TARGB(999),DADOT(999),DADOTG(999),
     8XLOLD(4,999),VISREX(999),TATMS(999),HTMP(999),DHTMP(999),
     9HTMOLD(999)
      COMMON/CATCHI/NUMS(4,999),NUMOLD(4,999),NTRACK(16),NSUMP(999),
     1NPASSG(999),NCRSTM(999),NCRRT(999),NPASS(999),NBURN,NBURNO     
      COMMON/PRINTB/ELEVAT(999),ELOLD(999),XDIST(999,999),XBTW(999,999),
     1TEMP(999,999),ENTHP(999,999),EOLD(999,999),ENBLK(999),EBKOLD(999),
     2TBULK(999),HITE(16,999),HITOLD(16,999),HTOT(999),HTOLD(999),
     3HTCFT(999),HTCOEF(999),QFLUXT(999),QFLUXB(999),VEL(999),ELO(999),
     4VELOLD(999),SRSCOR(16),AREA(999),RAD(999),ARC(999),VOLCN(16),
     5VOID(999),VGJ(999),ZABLAT(999),ZABOLD(999),DCRUST(999),
     6DCROLD(999),DABCON(999),DABOLD(999),DFILMT(999),DFOLT(999),
     7SMFLX(4,999),XLSMF(4,999),TOTVOL,XFACMS(999),XMFLXA,XMCORT,
     8VCORT,TOTOX,TOTMET,QFLXT,QFLXB,TIME,DTIME,XMCOR(16),VCOR(16),
     9TCONI,RSAND,HDOWNC,TBOUND,EMISCN,PDRYWL,XDISTO(999),QOXT(999),
     1XLENSH,XBTWO(999),RCOMP,WDOOR,RSUMP,RSHELL,RPED,TPED,ELSMP,
     1TENDP(10,999),TFRZSH,TDEBRS,TKDEBR,PDEBR,CPDEBR,ENENDP(10,999),
     2ENOLDP(10,999),DXVERT,DXSNK,XDSTE(10,999),XBTE(10,999),HXLA(999),
     3HXLB(999),TSFEB(999),DCRS(999),DCRSLD(999),HCRS(16,999),
     4HCRSLD(16,999),THETE0(10,999),THETE1(10,999),QSHELL,QSHELE,TSHELI,
     5SIGOXE(999),FKOXE(999),RINJC,XLSEC,ANGSEC,ANINJC,XLCHAN,WCHNL,
     6TEFZX(999),TIMSPC(999),DXNODE(999),TNORM(999),FRCSOL(999),
     7ALPSPR(999),CRAMCON,HINTF,TSHLMX,XFRMX(999),XFROX(999),XFRTX(999),
     8XMLMX(999),XMLOX(999),XMLTX(999),XTOTX(999),TIMEO,TMAX,EDOWN(999),
     9HCP(999)
      COMMON/HCONS/HMETAL(999),HOXIDE(999)  
      COMMON/PROPI/ NMATC,NMATFI,NMATFF,ICAOH2,ICACO3,IMCCO3,IFH2O,     
     1IVH2O,ICK2O,IVK2O,INA2O,ITIO2,ISIO2,ICAO,IMGO,IAL2O3,IFEO,IFE2O3, 
     2IFE3O4,IFE,ICR,INI,IZR,IU,IB4C,IB,ICR2O3,INIO,IUO2,IZRO2,IB2O3,   
     3NBCINT(999),NODES(999),NUMNOD,IMOX(28),IBAS,ILCS,ILL,NACTIV(999),     
     4NBFRZM,NBFRZO,NABLFM,NDRNFM,NTYPMT(999,999),NDOOR,NBMADJ,NUMSHV,   
     5NBSHL(999),NCRSTS(999),NUMSHH,NSKIPE,NACSH,NWAT,NPEND,NBCBOT,
     6NCRTOP,NBFZOE,NBFZME,NGEOM,NL(999,999),NFRSCT,NLOGSH,NADAB,NINVIS,
     7NSIMST,NSTEEL,NOVHT,NOVTK,NOVUM,NOVEM,NOVSIG,NPLLOC(999),NBPL,
     8IXP(999),IYP(999),NVTPE,NSOLTP,NSOLF,NINTF,IFLGA(999),ICTYPE,ICTC,
     9NPOURS,NSMP,NPED,NDOR,NSHL,NANULS,NSMPCV,NBOIL,NSWALL,NMVER,
     1NCRTEM,NTHINC,NVELP,NITMAX,NENMAX,NPRINT,NPFREQ,NTIMSPC,NPLFREQ,
     2NPLTOT,NJET,NJETP,NJETD,NJETND,NODCAP,NVELPW,NITMAXW,NENMXW,
     3IFLGJ(999),NBEDCQ,ISHELE
      COMMON/PRINTR/ QFEH2O,QCRH2O,QZRH2O,QFECO2,QCRCO2,QZRCO2,
     4XFH2OU,XFCAOH,XFMGCA,XFCACO,XZRMRE,XFEMRE,XCRMRE,XZRORE,XFEORE,   
     5XCRORE,XMH2O,XMCO2,XMCACO,XMMGCA,XMCAOH,TFWL,TFWS,TBWL,TBWS,      
     6TMCAL,TMCAS,TCAL,TCAS,TFOS,TFOL,TFMS,TFML,XVISC(28),SVISC(28),    
     7XMOL(28),FMMOL(28),ROM(28),ROMLIQ(28),AEQM(28,2),BEQM(28,2),
     8CEQM(28,2),ECL,ECS,ECAL,ECAS,EMCAL,EMCAS,EBWL,EBWS,EFWL,EFWS,    
     9STEF,GRAV,PI,TCS,TCL,CCL,CCS,RMASSL,WPCC,WPM,WPA,WPCS,ROC,RMASSS,   
     1ANGSHL,RSLAGL,RSLAGS,HNODT,VFAV,VGAV,QXAV,XWTSS(16),  
     2TSCS(2),TSCL(2),ESCS(2),ESCL(2),CCSS(2),CCSL(2),ROSTLS,ROSTLL,
     3XFRGAS,HMINC,TST(99),TSTOP(99),AINTP(99),BINTP(99),DRATIO(999),
     4XBCN(999),XDCN(999),XBLT(15),ADEC(99),BDEC(99),APOUR(16,99),
     5BPOUR(16,99),XWTC(14),BWIDTH,THCKCV,TMBOIL,TEBOIL,VFINT,ANGFAN,
     6ALPMAX,THSHL,XFCABL,XNDMIN,DVMX,DAVMX,DEAVMX,DEMX,TDC,QDCU,QDCUO2,
     7TSINJ,EINJ,DTINJ,TKINJ,ROEV,CPINJ,SURFT,VSINJ,EMINJ,TSINJO,EINJO,
     8DTINJO,TKINJO,ROEVO,CPINJO,SURFTO,VSINJO,EMINJO,XLEADE,ARSUM,
     9DBUBOX,UTRISE,TREMSH,DJET,DFALL,WEBER,FROUDE,XPSAITO,XPEPSTN,
     1FRAG,XMBEDT,XMBED(16),XLPENT,XLPENA,UJET,UFALL,HFALL,ERPV,
     2TJETT(99),DJETT(99),HWATP,XDOTJET,DVMXW,DAVMXW,DEMXW,DEAVMXW,
     3QJETW,XSTMJF,TINTSJF,ESAT,DRDOOR,DRANL,EI,QDCBUO2,QDCBU,
     4EBEDS,EBEDB
      COMMON/BASPR/ HBMT(999,999,16),HBMOLD(999,999,16) 
      COMMON/PROPB/CPBM(999),FKB(999,999),DENB(999,999),DTDEB(999,999),
     1EMIBM(999),DENCRS(999),ENCRS(999),FKRF(999),DENRF(999),CPRF(999) 
      COMMON/PROPM/DTDEM(999),CPMLT(999),FKMLT(999),DENMLT(999),
     1SIGMLT(999),UMMLT(999),EMIMLT(999)      
      DATA TREF /298.15D0/      
C.....HERE WE GO.... 
      DO 4450 KH=1,NUMNOD
      NBFRZ=0
C.....FIRST SEE IF THE NODE IS ACTIVE AND IF MELT IS LOCALLY PRESENT.
      IF(NACTIV(KH).EQ.0.OR.NADAB.EQ.1) GO TO 4450      
      IF(HTOT(KH).LE.0.D0) GO TO 4450     
C.....NODE IS ACTIVE AND MELT IS HERE; NOW CHECK TO SEE IF SOLIDS 
C.....FRACTION HAS REACHED THE MAXIMUM ALLOWABLE AND IF NOT, BYPASS.
C.....IF BULK FREEZING IS POSSIBLE.
      IF(ALPSPR(KH).LT.ALPMAX) GO TO 4450
C.....MELT HAS REACHED THE SOLIDUS POINT, NODALIZED THE MATERIAL INTO
C.....THE BASEMAT.
      IF(NBCINT(KH).LE.3) GO TO 4453      
      ZMIN=XDISTO(KH)-XBTWO(KH) 
      IF(ZABLAT(KH).GT.ZMIN) GO TO 4453   
      ZBOLD=ZABLAT(KH)
      NODES(KH)=NODES(KH)-1     
      ZABLAT(KH)=ZABLAT(KH)+XDIST(2,KH)   
      ZABOLD(KH)=ZABLAT(KH)
      XDISTO(KH)=XDIST(2,KH)+ZBOLD
      XBTWO(KH)=XBTW(2,KH)+ZBOLD
      DO 4454 KGS=1,4 
      IF(NUMS(KGS,KH).GT.2) GO TO 4921   
      IF(NUMS(KGS,KH).EQ.2) XLSMF(KGS,KH)=XLSMF(KGS,KH)+ZBOLD
      NUMS(KGS,KH)=1  
      GO TO 4454      
 4921 CONTINUE
      NUMS(KGS,KH)=NUMS(KGS,KH)-1
 4454 CONTINUE
      DO 4455 KV=1,NODES(KH)    
      ZMULT=1.D0      
      IF(KV.EQ.1) ZMULT=(ZBOLD+XBTW(2,KH))/XBTW(2,KH)
      DO 4457 KTP=1,16
      HBMT(KV,KH,KTP)=HBMT(KV+1,KH,KTP)*ZMULT
 4457 CONTINUE
      DTDEB(KV,KH)=DTDEB(KV+1,KH)
      NTYPMT(KV,KH)=NTYPMT(KV+1,KH)
      NL(KV,KH)=NL(KV+1,KH)     
      IF(KV.GT.1) GO TO 4458    
      XDIST(KV,KH)=XDIST(KV+1,KH)+ZBOLD   
      XBTW(KV,KH)=XBTW(KV+1,KH)+ZBOLD     
      GO TO 4455      
 4458 CONTINUE
      XDIST(KV,KH)=XDIST(KV+1,KH)
      XBTW(KV,KH)=XBTW(KV+1,KH) 
      TEMP(KV,KH)=TEMP(KV+1,KH) 
      ENTHP(KV,KH)=ENTHP(KV+1,KH)
      EMAX(KV,KH)=EMAX(KV+1,KH) 
 4455 CONTINUE
 4453 CONTINUE
C.....PERFORM BULK FREEZING DEPENDING UPON WHETHER FREEZING OCCURS      
C.....OVER CONRETE OR STEEL AS OPPOSED TO DEBRIS, AND WHETHER OR NOT    
C.....A CRUST AND/OR FILM BOUNDARY CONDITION IS CURRENTLY APPLICABLE    
C.....AT THE INTERFACE.
      IF(NBCINT(KH).EQ.2.OR.NBCINT(KH).EQ.6.OR.NBCINT(KH).EQ.8) GO TO   
     1 6723 
      ROCCR=0.D0      
      ECRST=0.D0      
      GO TO 6724      
 6723 CONTINUE
      CALL MASFRA(HCRUST,ROM,KH,RSLAGS,ROC,ROCCR,0) 
      CALL ASINEA(HCRUST,KH,HSEND,ROM,WPCC,WPM,WPA,WPCS,ROC,RSLAGS,0)   
      CALL ETF(ECRST,HSEND,TFZX(KH),DTDE) 
 6724 CONTINUE
      IF(NBCINT(KH).EQ.5.OR.NBCINT(KH).EQ.8.OR.NBCINT(KH).EQ.9) GO TO   
     1 7319 
      DELFD=0.D0      
      ROCFLM=0.D0     
      ECFILM=0.D0     
      GO  TO 7321     
 7319 CONTINUE
      CALL MASFRA(DFILM,ROMLIQ,KH,RSLAGL,ROC,ROCFLM,0)  
      DELFD=1.D0/DRATIO(KH)     
      IF(NBCINT(KH).EQ.5) GO TO 7323      
      IF(NBCINT(KH).EQ.8) GO TO 8318      
      TINT=TFZC(KH)+(HTCOEF(KH)*DFILMT(KH)*(TBULK(KH)-TFZX(KH)))/TKM(KH)
      GO TO 7325      
 8318 CONTINUE
      C1=TKX(KH)/DCRUST(KH)+TKM(KH)/DFILMT(KH)      
      C2=(TKX(KH)*TFZX(KH))/DCRUST(KH)+(TKM(KH)*TFZC(KH))/DFILMT(KH)    
      TINT=C2/C1      
      GO TO 7325      
 7323 CONTINUE
      TINT=(HTCOEF(KH)*TBULK(KH)+(TKM(KH)*TFZC(KH))/DFILMT(KH))/(HTCOEF(
     1KH)+TKM(KH)/DFILMT(KH))   
 7325 CONTINUE
      CALL ASINEA(DFILM,KH,HSEND,ROMLIQ,WPCC,WPM,WPA,WPCS,ROC,RSLAGL,0) 
      CALL ETF(ECFILM,HSEND,TINT,DTDE)    
 7321 CONTINUE
      CALL MASFRA(HITE,ROM,KH,RSLAGS,ROC,ROCBS,0)   
      CALL MASFRA(HITE,ROMLIQ,KH,RSLAGL,ROC,ROCBL,0)
      DELEL=ROCBL*HTOT(KH)/ROCBS
      IF(NTYPMT(1,KH).LE.3) GO TO 6725    
C.....BULK FREEZING OVER PREVIOUSLY SOLIDIFIED MATERIAL DONE HERE.      
      CALL MASFRD(ROM,1,KH,RSLAGS,ROC,ROCBM)
      XLENBM=ZABLAT(KH)-(XDISTO(KH)-XBTWO(KH))      
      XLENBM=DMAX1(XLENBM,0.D0) 
      HHAT=DELEL+DCRUST(KH)+XLENBM+DELFD*DFILMT(KH) 
C.....TEMPORARILY ASSIGN FROZEN MATERIAL TO CRUST ARRAY FOR EVALUATION  
C.....OF HOMOGENEOUS ENTHALPY AND TEMPERATURE.      
      HSUMT=0.D0      
      HSUMO=0.D0      
      DO 6326 KTP=1,16
      CALL INDEX(KTP,KJ)
      RDRAT=ROMLIQ(KJ)/ROM(KJ)  
      IF(KTP.EQ.16) RDRAT=RSLAGL/RSLAGS   
      HCRUST(KTP,KH)=HCRUST(KTP,KH)+HITE(KTP,KH)*RDRAT+HBMT(1,KH,KTP)   
     1+DFILM(KTP,KH)*RDRAT      
      HSUMT=HSUMT+HCRUST(KTP,KH)
      IF(KTP.GE.8) HSUMO=HSUMO+HCRUST(KTP,KH)
 6326 CONTINUE
      FRACO=HSUMO/HSUMT
      NTBM=4
      IF(FRACO.GE.0.5) NTBM=5   
      CALL MASFRA(HCRUST,ROM,KH,RSLAGS,ROC,ROCHAT,0)
      ESTORE=(ROCBM*ENTHP(1,KH)*XLENBM+ROCCR*ECRST*DCRUST(KH)+ROCFLM*ECF
     1ILM*DFILMT(KH)+ROCBL*ENBLK(KH)*HTOT(KH))/(HHAT*ROCHAT)  
      CALL ASINEA(HCRUST,KH,HSEND,ROM,WPCC,WPM,WPA,WPCS,ROC,RSLAGS,0)   
      CALL TEF(ESTORE,HSEND,TSTORE,DTDE)  
C.....CALCULATE TOTAL NUMBER OF NODES CREATED DUE TO SOLIDIFICATION.    
      XNDMCK=XNDMIN   
      HMFRZ=0.5*HMINC
      IHOLE=IDINT(HHAT/XNDMIN)+1 
      HRESID=HHAT-XNDMIN*FLOAT(IHOLE-1)   
      IF(IHOLE.EQ.1) GO TO 6330 
      IF(HRESID.GT.HMFRZ) GO TO 6330      
      XNDMCK=HHAT/FLOAT(IHOLE-1)
      HRESID=XNDMCK   
      IHOLE=IHOLE-1   
 6330 CONTINUE
      DGAS=HRESID-XLENBM
      IF(IHOLE.GT.1) DGAS=0.5D0*XNDMCK-XLENBM
      DGAS=DMAX1(DGAS,0.D0)     
      NSAVE=NODES(KH) 
      NODES(KH)=NODES(KH)+IHOLE-1
      IF(NODES(KH).GT.NODCAP) NODES(KH)=NODCAP      
C.....SHIFT NODES TO INCORPORATE SOLIDIFIED MATERIAL.
      IF(IHOLE.EQ.1) GO TO 6331 
      IPLS2=2
      DO 6332 KDR=2,NSAVE
      IBN=NODES(KH)-KDR+IPLS2   
      IBO=NSAVE-KDR+IPLS2
      DO 6334 KTP=1,16
      HBMT(IBN,KH,KTP)=HBMT(IBO,KH,KTP)   
 6334 CONTINUE
      DTDEB(IBN,KH)=DTDEB(IBO,KH)
      XDIST(IBN,KH)=XDIST(IBO,KH)
      XBTW(IBN,KH)=XBTW(IBO,KH) 
      TEMP(IBN,KH)=TEMP(IBO,KH) 
      ENTHP(IBN,KH)=ENTHP(IBO,KH)
      NTYPMT(IBN,KH)=NTYPMT(IBO,KH)
      NL(IBN,KH)=NL(IBO,KH)     
      EMAX(IBN,KH)=EMAX(IBO,KH) 
 6332 CONTINUE
 6331 CONTINUE
C.....ASSIGN NODAL VARIABLES IN SOLIDIFIED REGION.  
      DO 6335 KDR=1,IHOLE
      TEMP(KDR,KH)=TSTORE
      ENTHP(KDR,KH)=ESTORE      
      DTDEB(KDR,KH)=DTDE
      NL(KDR,KH)=0    
      NTYPMT(KDR,KH)=NTBM
      XBTW(KDR,KH)=HRESID
      IF(KDR.GT.1) XBTW(KDR,KH)=0.5D0*XNDMCK
      XDIST(KDR,KH)=XNDMCK      
      IF(KDR.EQ.1.AND.IHOLE.GT.1) XDIST(KDR,KH)=HRESID+0.5D0*XNDMCK     
      IF(KDR.EQ.1.AND.IHOLE.EQ.1) XDIST(KDR,KH)=HRESID+XDISTO(KH)-XBTWO(
     1KH)   
      IF(KDR.EQ.IHOLE.AND.IHOLE.GT.1) XDIST(KDR,KH)=0.5D0*XNDMCK+
     1 XDISTO(KH)-XBTWO(KH)     
      VOLUME=XBTW(1,KH)
      IF(KDR.GT.1) VOLUME=XDIST(KDR-1,KH)-XBTW(KDR-1,KH)+XBTW(KDR,KH)   
      DO 6336 KTP=1,16
      HBMT(KDR,KH,KTP)=VOLUME*HCRUST(KTP,KH)/HSUMT  
 6336 CONTINUE
 6335 CONTINUE
      NBCINT(KH)=1    
      ELEVAT(KH)=ELEVAT(KH)+DELEL
      XDISTO(KH)=XDIST(1,KH)    
      XBTWO(KH)=XBTW(1,KH)      
      ZABLAT(KH)=XDISTO(KH)     
      ZABOLD(KH)=ZABLAT(KH)     
      DCRUST(KH)=0.D0 
      TBULK(KH)=TREF  
      ENBLK(KH)=0.D0  
      DTDEM(KH)=0.D0  
      HTOT(KH)=0.D0   
      HMETAL(KH)=0.D0 
      HOXIDE(KH)=0.D0 
      DFILMT(KH)=0.D0 
      DO 6338 KTP=1,16
      HCRUST(KTP,KH)=0.D0
      DFILM(KTP,KH)=0.D0
      HITE(KTP,KH)=0.D0
 6338 CONTINUE
      DO 6337 KGS=1,4 
      IF(NUMS(KGS,KH).GT.1) GO TO 6339    
      XLSMF(KGS,KH)=XLSMF(KGS,KH)+DGAS    
 6339 CONTINUE
      NUMS(KGS,KH)=NUMS(KGS,KH)+IHOLE-1   
 6337 CONTINUE
      GO TO 4450      
C.....PERFORM BULK FREEZING PROCEDURE FOR CASE IN WHICH FREEZING OVER   
C.....A CONCRETE OR STEEL SURFACE OCCURS. 
 6725 CONTINUE
      ESTORE=ENTHP(1,KH)
      TSTORE=TEMP(1,KH)
      XMID=0.5D0*ZABLAT(KH)-0.5D0*(XDISTO(KH)-XBTWO(KH))      
      HHAT=DELEL+DCRUST(KH)+DFILMT(KH)*DELFD
      HSUMT=0.D0      
      HSUMO=0.D0      
      DO 7326 KTP=1,16
      CALL INDEX(KTP,KJ)
      RDRAT=ROMLIQ(KJ)/ROM(KJ)  
      IF(KTP.EQ.16) RDRAT=RSLAGL/RSLAGS   
      HCRUST(KTP,KH)=HCRUST(KTP,KH)+(HITE(KTP,KH)+DFILM(KTP,KH))*RDRAT  
      HSUMT=HSUMT+HCRUST(KTP,KH)
      IF(KTP.GE.8) HSUMO=HSUMO+HCRUST(KTP,KH)
 7326 CONTINUE
      FRACO=HSUMO/HSUMT
      NTBM=4
      IF(FRACO.GE.0.5) NTBM=5   
C.....HELLO!!!
      CALL MASFRA(HCRUST,ROM,KH,RSLAGS,ROC,ROCHAT,0)
      ENTHP(1,KH)=(ROCCR*ECRST*DCRUST(KH)+DFILMT(KH)*ROCFLM*ECFILM      
     1+ROCBL*ENBLK(KH)*HTOT(KH))/(HHAT*ROCHAT)      
      CALL ASINEA(HCRUST,KH,HSEND,ROM,WPCC,WPM,WPA,WPCS,ROC,RSLAGS,0)   
      CALL TEF(ENTHP(1,KH),HSEND,TEMP(1,KH),DTDEML) 
      XNDMCK=XNDMIN   
      HMFRZ=XNDMIN
      IHOLE=IDINT(HHAT/XNDMIN)+1 
      HRESID=HHAT-XNDMIN*FLOAT(IHOLE-1)   
      IF(IHOLE.EQ.1) GO TO 7330 
      IF(HRESID.GT.HMFRZ) GO TO 7330      
      XNDMCK=HHAT/FLOAT(IHOLE-1)
      HRESID=XNDMCK   
      IHOLE=IHOLE-1   
 7330 CONTINUE
C.....SHIFT GAS ISOTHERMS TO ACCOUNT FOR SOLIDIFIED MATERIAL. 
      DO 8383 KGS=1,4 
      IF(NUMS(KGS,KH).GT.1) GO TO 8382    
      XLMOVE=XLSMF(KGS,KH)-XMID 
      IF(XLMOVE.GT.0.D0) GO TO 8711
      XLSMF(KGS,KH)=XLSMF(KGS,KH)+HRESID  
      IF(IHOLE.GT.1) XLSMF(KGS,KH)=XLSMF(KGS,KH)+0.5D0*XNDMCK 
      NUMS(KGS,KH)=NUMS(KGS,KH)+IHOLE-1   
      GO TO 8383      
 8711 CONTINUE
      XLSMF(KGS,KH)=XLSMF(KGS,KH)-XMID    
      NUMS(KGS,KH)=NUMS(KGS,KH)+IHOLE     
      GO TO 8383      
 8382 CONTINUE
      NUMS(KGS,KH)=NUMS(KGS,KH)+IHOLE     
 8383 CONTINUE
C.....SHIFT NODES TO ACCOUNT FOR SOLIDIFYING MATERIAL.
      NSAVE=NODES(KH) 
      NODES(KH)=NODES(KH)+IHOLE 
      IF(NODES(KH).GT.NODCAP) NODES(KH)=NODCAP      
      IPLS2=2
      DO 7332 KDR=2,NSAVE
      IBN=NODES(KH)-KDR+IPLS2   
      IBO=NSAVE-KDR+IPLS2
      DO 7334 KTP=1,16
      HBMT(IBN,KH,KTP)=HBMT(IBO,KH,KTP)   
 7334 CONTINUE
      DTDEB(IBN,KH)=DTDEB(IBO,KH)
      XDIST(IBN,KH)=XDIST(IBO,KH)
      XBTW(IBN,KH)=XBTW(IBO,KH) 
      TEMP(IBN,KH)=TEMP(IBO,KH) 
      ENTHP(IBN,KH)=ENTHP(IBO,KH)
      NTYPMT(IBN,KH)=NTYPMT(IBO,KH)
      NL(IBN,KH)=NL(IBO,KH)     
      EMAX(IBN,KH)=EMAX(IBO,KH) 
 7332 CONTINUE
C.....NODALIZE THE SOLDIFIED MATERIAL INTO THE BASEMAT.
      IUP=IHOLE+1     
      XDIST(IUP,KH)=XDISTO(KH)-XBTWO(KH)+XMID
      XBTW(IUP,KH)=XMID
      FMULTF=1.D0+(XMID/ZABLAT(KH))*(TEMP(2,KH)/TSTORE-1.D0)  
      ENTHP(IUP,KH)=ESTORE*FMULTF
      EMAX(IUP,KH)=EMAX(1,KH)*FMULTF      
      CALL TEC(ENTHP(IUP,KH),EMAX(IUP,KH),TEMP(IUP,KH),DTDE,RMASS,YCACO3
     1,YMCCO3,YCAOH2,YFH2O,NL(1,KH))      
      DTDEB(IUP,KH)=DTDE
      NTYPMT(IUP,KH)=NTYPMT(1,KH)
      NL(IUP,KH)=NL(1,KH)
      DO 7335 KDR=1,IHOLE
      DTDEB(KDR,KH)=DTDEML      
      TEMP(KDR,KH)=TEMP(1,KH)   
      ENTHP(KDR,KH)=ENTHP(1,KH) 
      NTYPMT(KDR,KH)=NTBM
      NL(KDR,KH)=0    
      XBTW(KDR,KH)=HRESID
      IF(KDR.GT.1) XBTW(KDR,KH)=0.5D0*XNDMCK
      XDIST(KDR,KH)=XNDMCK      
      IF(KDR.EQ.1.AND.IHOLE.GT.1) XDIST(KDR,KH)=HRESID+0.5D0*XNDMCK     
      IF(KDR.EQ.1.AND.IHOLE.EQ.1) XDIST(KDR,KH)=HRESID+XMID   
      IF(KDR.EQ.IHOLE.AND.IHOLE.GT.1) XDIST(KDR,KH)=0.5D0*XNDMCK+XMID   
      VOLUME=XBTW(1,KH)
      IF(KDR.GT.1) VOLUME=XDIST(KDR-1,KH)-XBTW(KDR-1,KH)+XBTW(KDR,KH)   
      DO 7336 KTP=1,16
      HBMT(KDR,KH,KTP)=VOLUME*HCRUST(KTP,KH)/HSUMT  
 7336 CONTINUE
 7335 CONTINUE
      NBCINT(KH)=1    
      ELEVAT(KH)=ELEVAT(KH)+DELEL
      XDISTO(KH)=XDIST(1,KH)    
      XBTWO(KH)=XBTW(1,KH)      
      ZABLAT(KH)=XDISTO(KH)     
      ZABOLD(KH)=ZABLAT(KH)
      TBULK(KH)=TREF  
      ENBLK(KH)=0.D0  
      DTDEM(KH)=0.D0  
      DCRUST(KH)=0.D0 
      HTOT(KH)=0.D0   
      HMETAL(KH)=0.D0 
      HOXIDE(KH)=0.D0 
      DFILMT(KH)=0.D0 
      DO 7338 KTP=1,16
      HCRUST(KTP,KH)=0.D0
      HITE(KTP,KH)=0.D0
      DFILM(KTP,KH)=0.D0
 7338 CONTINUE
 4450 CONTINUE
      RETURN
      END