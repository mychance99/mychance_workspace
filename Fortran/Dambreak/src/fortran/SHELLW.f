C...................................................................... 
C.....SUBROUTINE SHELLW EVALUATES THE DRYWELL SHELL-TO-WATER HEAT
C.....TRANSFER COEFFICIENT ALONG THE FULL BOILING CURVE USING THE
C.....ROHSENOW EXPRESSION AS MODIFIED BY JUNG ET AL. FOR INCLINED
C.....SURFACES IN NUCLEATE BOILING, AND THE RESULTS OF SAUER ET AL.     
C.....FOR FILM BOILING ON INCLINED SURFACES.  ZUBER'S CHF CORRELATION   
C.....IS USED TO CHARACTERIZE THE CHF POINT, AND HENRY'S CORRELATION    
C.....IS USED TO CHARACTERIZE THE MINIMUM FILM BOILING TEMPERATURE.     
C.....THE HEAT FLUX IN THE TRANSITION BOILING REGIME IS FOUND BY
C.....LINEAR INTERPOLATION BETWEEN THE CHF AND MINIMUM FILM BOILING     
C.....POINTS.
C...................................................................... 
      SUBROUTINE SHELLW(TWABS,TSUB,PDRYWL,HTC,ANGSHL)    
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      REAL(8) KL,KV
      COMMON/DATSIMS/TSIS,TSIL,CSIS,CSIL,DHSIL,RSIS,RSIL,TKSIS,TKSIL,
     1ESIL,XCSI(16)
      DATA GRAV/9.82D0/
      E13=1.D0/3.D0   
      E23=2.D0/3.D0   
      CALL CONWAT(TSAT,KL,PL,CL,UL,HLV,EWL,EWV,CWV,SIGMA,PDRYWL)
      TSUP=TWABS-TSAT 
      HTC=0.D0
      IF(TSUP.LE.0.D0) GO TO 105
      CSF=7.218D-3-1.74D-6*ANGSHL
      AN2=ANGSHL*ANGSHL
      AN3=ANGSHL*AN2  
      RX=.256D0-1.514D-4*ANGSHL+1.77D-5*AN2-7.16D-8*AN3
      RXI=1.D0/RX     
      RXS=1.7D0*RXI   
      FBF=1.D0+4.5139D-3*ANGSHL 
      IF(TWABS.GT.TSAT) GO TO 1000
      HTC=0.D0
      GO TO 105
 1000 CONTINUE
      TAVE=0.5D0*(TWABS+TSAT)   
      CALL CONH2O(TAVE,PDRYWL,CV,UV,KV,PV)
C.....DETERMINE IF BOILING REGIME IS NUCLEATE, TRANSITION, OR FILM      
C.....BOILING.
      DP=PL-PV
      C1=1.D0+(0.1D0*((PL/PV)**0.75D0)*CL*TSUB)/HLV 
      C2=0.14D0*PV*HLV*(((GRAV*SIGMA*DP)/PV**2)**0.25D0)      
      QCHF=C1*C2      
      C3=SQRT(SIGMA/(GRAV*DP))  
      C4=(KL/(CL*UL))**RXS      
      C5=(CL/(HLV*CSF))**RXI    
      C6=(UL*HLV)/C3  
      ANB=C4*C5*C6    
      DTCHF=(QCHF/ANB)**RX      
      IF(TSUP.GT.DTCHF) GO TO 50
C.....BOILING REGIME IS NUCLEATE...........
      RXE=RXI
      HTC=ANB*TSUP**RXE
      GO TO 105
   50 CONTINUE
      C6=(0.127D0*HLV*PV)/KV    
      C7=((GRAV*DP)/(PL+PV))**E23
      C8=(UV/(GRAV*DP))**E13    
      DTBER=C6*C7*C3*C8
      CALL DENSS(TWABS,PM)      
      CALL CONDSS(TWABS,FKM)    
      CALL ETSS(EPS,TWABS,DTDE) 
      CM=1.D0/DTDE    
      C9=SQRT((KL*PL*CL)/(FKM*PM*CM))     
      C10=((C9*HLV)/(CM*DTBER))**0.6D0    
      DTMIN=DTBER+.42D0*C10*(DTBER+TSUB)  
      IF(DTMIN.LE.DTCHF) DTMIN=DTCHF+10.D0
      C11=((HLV*PV*GRAV*DP*KV*KV*KV)/(C3*UV))**.25D0
      C12=.425D0*C11  
      IF(TSUP.GT.DTMIN) GO TO 75
C.....BOILING REGIME IS TRANSITION.............     
      QMIN=FBF*C12*TSUP**.75D0  
      QTRAN=((QMIN-QCHF)/(DTMIN-DTCHF))*(TSUP-DTCHF)+QCHF     
      HTC=QTRAN/TSUP  
      GO TO 105
   75 CONTINUE
C.....BOILING REGIME IS FILM............  
      HTC=(FBF*C12)/TSUP**0.25D0
  105 CONTINUE
      RETURN
      END